// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack
/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static int KBD;

    /** Initializes the keyboard. */
    function void init() {
        let KBD = 24576; //KBDポインタはアドレス24576を返す
        
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(KBD);
    }

    /**	Waits until a key is pressed on the keyboard and released,
     *  then echoes the key to the screen, and returns the character 
     *  of the pressed key. */
    function char readChar() {
        var char c;
        while(Keyboard.keyPressed() = 0){} //キー入力がない場合何もしない

        let c = Keyboard.keyPressed();

        while(~(Keyboard.keyPressed() = 0)){} //キー入力の終了を待つ

        do Output.printChar(c);

        return c;
    }

    /**	Displays the message on the screen, reads from the keyboard the entered
     *  text until a newline character is detected, echoes the text to the screen,
     *  and returns its value. Also handles user backspaces. */
    function String readLine(String message) {
        var char c;
        var String input;
        
        do Output.printString(message);
        
        let input = String.new(128);
        let c = Keyboard.readChar();

        while(~(c = String.newLine())){
            if (c = String.backSpace()){
                if(input.length() > 0){
                    do input.eraseLastChar();
                }
            }else{
                do input.appendChar(c);
            }
            
            let c = Keyboard.readChar();
        }
        
        return input; 
    }   

    /** Displays the message on the screen, reads from the keyboard the entered
     *  text until a newline character is detected, echoes the text to the screen,
     *  and returns its integer value (until the first non-digit character in the
     *  entered text is detected). Also handles user backspaces. */
    function int readInt(String message) {
        var int i;
        var boolean isMinus;
        var char c;
        var String input;
        
        do Output.printString(message);
        
        let input = String.new(128);
        let c = Keyboard.readChar();

        while(~(c = String.newLine())){
            if (c = String.backSpace()){
                if(input.length() > 0){
                    do input.eraseLastChar();
                }
            }else{
                do input.appendChar(c);
            }
            
            let c = Keyboard.readChar();
        }

        let isMinus = input.charAt(0) = 45;  // 45='-'
        if(isMinus){
           let i = 1; 
           while(i < input.length()){
              do input.setCharAt(i-1,input.charAt(i));
              let i = i + 1;
           }
           do input.eraseLastChar();
           return (~input.intValue()) + 1;
        }
        
        return input.intValue(); 
    }
}
